*vim-leader-guide.txt*      A vim key-guide.
*leader-guide*

==============================================================================
CONTENTS                                                 *leader-guide-contents*

Overview                                    |leader-guide-overview|
Usage examples                              |leader-guide-examples|

Commands                                    |leader-guide-commands|
Functions                                   |leader-guide-functions|
Configuration                               |leader-guide-configuration|

CHANGELOG                                   |leader-guide-changelog|


==============================================================================
OVERVIEW                                                 *leader-guide-overview*

|vim-leader-guide| is a plugin to explore keymaps.
The name is a slight misnomer. The guide does work for all keys, as well as
global mappings. Not just for <leader>.

Features:
  - Show keymaps assigned to a specific prefix, such as leader, localleader or
    any other key.
  - Show all global and buffer-local keymaps.
  - Provide additional descriptions for mappings and groups of mappings


==============================================================================
USAGE EXAMPLES                                           *leader-guide-examples*

Basic Configuration~
>
    " Define prefix dictionary
    let g:lmap =  {}

    " Second level dictionarys:
    let g:lmap.f = { 'name' : 'File Menu' }
    let g:lmap.o = { 'name' : 'Open Stuff' }
    " 'name' is a special field. It will define the name of the group.
    " leader-f is the "File Menu" group.
    " Unnamed groups will show an empty string

    " Provide commands and descriptions for existing mappings
    nmap <silent> <leader>fd :e $MYVIMRC<CR>
    let g:lmap.f.d = ['e $MYVIMRC', 'Open vimrc']

    nmap <silent> <leader>fs :so %<CR>
    " let g:lmap.f.s = ['so %', 'Source file']

    nmap <silent> <leader>oo  :copen<CR>
    " let g:lmap.o.o = ['copen', 'Open quickfix']

    nmap <silent> <leader>ol  :lopen<CR>
    " let g:lmap.o.l = ['lopen', 'Open locationlist']

    nmap <silent> <leader>fw :w<CR>
    " let g:lmap.f.w = ['w', 'Write file']
<
    Note: Depending on your use case extensive dictionary configuration might
    not be necessary.

    Instead of >
    nmap <silent> <leader>fd :e $MYVIMRC<CR>
    let g:lmap.f.d = ['e $MYVIMRC', 'Open vimrc']
<
    one could simply use: >
    nnoremap <Plug>(open-vimrc) :e $MYVIMRC<CR>
    nmap <leader>fd <Plug>(open-vimrc)
<
    This variant is a lot less intrusive and just as descriptive as the
    dictionary variant above.
    This way it is possible to limit the dictionarys to group names.

    Note: The <Plug> prefix can be hidden with:
    |g:leaderGuide_displayfunc|


Menu Function~
>
    " Create new menus not based on existing mappings:
    let g:lmap.g = {
                    \'name' : 'Git Menu',
                    \'s' : ['Gstatus', 'Git Status'],
                    \'p' : ['Gpull',   'Git Pull'],
                    \'u' : ['Gpush',   'Git Push'],
                    \'c' : ['Gcommit', 'Git Commit'],
                    \'w' : ['Gwrite',  'Git Write'],
                    \}

<
NerdCommenter~
>
    " If you use NERDCommenter:
    let g:lmap.c = { 'name' : 'Comments' }
    " Define some descriptions
    let g:lmap.c.c = ['call feedkeys("\<Plug>NERDCommenterComment")','Comment']
    let g:lmap.c[' '] = ['call feedkeys("\<Plug>NERDCommenterToggle")','Toggle']
    " The Descriptions for other mappings defined by NerdCommenter, will default
    " to their respective commands.

<
Plugin Specific Menus~
>
    " set up dictionary for <localleader>
    let g:llmap = {}
    autocmd FileType tex let g:llmap.l = { 'name' : 'vimtex' }
    call leaderGuide#register_prefix_descriptions(",", "g:llmap")
    " to name the <localleader>-n group vimtex in tex files.

<
Final Setup~
>
    " combine the two dictionarys into a single top-level dictionary:
    let g:topdict = {}
    let g:topdict[' '] = g:lmap
    let g:topdict[' ']['name'] = '<leader>'
    let g:topdict[','] = g:llmap
    let g:topdict[',']['name'] = '<localleader>'

    " register it with the guide:
    call leaderGuide#register_prefix_descriptions("", "g:topdict")

    " define mappings:
    nnoremap <silent> <leader> :<c-u>LeaderGuide '<Space>'<CR>
    vnoremap <silent> <leader> :<c-u>LeaderGuideVisual '<Space>'<CR>
    nnoremap <silent> <leader>. :<c-u>LeaderGuide '  '<CR>
    vnoremap <silent> <leader>. :<c-u>LeaderGuideVisual '  '<CR>
    nnoremap <localleader> :<c-u>LeaderGuide  ','<CR>
    vnoremap <localleader> :<c-u>LeaderGuideVisual  ','<CR>
    nnoremap <localleader>. :<c-u>LeaderGuide  '<buffer>'<CR>
    vnoremap <localleader>. :<c-u>LeaderGuideVisual  '<buffer>'<CR>
>

==============================================================================
COMMANDS                                                 *leader-guide-commands*

|vim-leader-guide| provides the following ex-commands:
    LeaderGuide '(String)'                          |LeaderGuide|
    LeaderGuideVisual '(String)'                    |LeaderGuideVisual|
    LeaderGuideD {dict}                             |LeaderGuideD|
    LeaderGuideVisualD {dict}                       |LeaderGuideVisualD|



                                                                 *LeaderGuide*
:LeaderGuide '{prefix}'

    Opens the guide window for the given prefix.
    This command, together with |LeaderGuideVisual| is the primary way of
    interacting with the plugin.

    Examples:
>
    nnoremap <leader> :LeaderGuide ' '<CR>
    nnoremap <localleader> :LeaderGuide ','<CR>
    nnoremap <leader>. :LeaderGuide '  '<CR>
    nnoremap <localleader>. :LeaderGuide '<buffer>'<CR>

    :LeaderGuide 'y'
    :LeaderGuide '<C-W>'
<
    In addition to normal and special keys, this command accepts the following
    reseved strings:
        
        - '<buffer>'            Lists all buffer-local mappings.
        - '  '                  Lists all global mappings.


                                                           *LeaderGuideVisual*
:LeaderGuideVisual '{prefix}'

    Same as |LeaderGuide|. Opens the guide in visual mode.

    Examples:
>
    vnoremap <leader> :LeaderGuide ' '<CR>
    etc.
<

                                                                *LeaderGuideD*
:LeaderGuideD {dict}

    Opens the guide window for a given dictionary directly.
    This command will not parse for mappings.

    This functionality can be used to build selection menus independent from
    native vim mappings.

    Examples:
>
    :LeaderGuideD g:mydict
<

    Note: To display keymaps use the |LeaderGuide| command, possible in
    combination with |leaderGuide#register_prefix_descriptions()|.

                                                          *LeaderGuideVisualD*
:LeaderGuideVisualD {dict}

    Same as |LeaderGuideD|, opening the menu in visual mode.


==============================================================================
FUNCTIONS                                               *leader-guide-functions*

                                  *leaderGuide#register_prefix_descriptions()*
leaderGuide#register_prefix_descriptions("{prefix}", "{dictname}")

    Provide the guide with a description dictionary. This dictionary is used
    to provide convenient names for mappings to display in the guide popup.
    Additionally it is used to provide group names.

    See also: |leader-guide-examples|

    Types:
        {prefix}:   String
        {dictname}: String

    Examples:
>
    " register dictionary for the <Space>-prefix
    call leaderGuide#register_prefix_descriptions(' ', "g:space_prefix_dict")
    " register dictionary for the ,-prefix
    call leaderGuide#register_prefix_descriptions(',', "g:comma_prefix_dict")

    " register a top-level dictionary:
    let g:space_prefix_dict['name'] = '<leader>'
    let g:space_prefix_dict[','] = '<localleader>'
    let g:globalguide = {}
    let g:globalguide[' '] = g:space_prefix_dict
    let g:globalguide[','] = g:comma_prefix_dict
    call leaderGuide#register_prefix_descriptions("", "g:globalguide")
<
    Note: To pass a top-level dictionary call the function with an empty
    prefix string

    Note: Please use either a single top-level dictionary, or one or more
    prefix specific dictionarys. Don't mix a top-level with prefix specific
    dictionarys.
    Instead, as shown above, insert the prefix specific dictionarys into the
    top-level one and pass only the latter to the guide.

    Note: After passing a dictionary name to the guide do not unlet the actual
    global dictionary. It is possible update the guide descriptions on the fly
    by changing g:dict.
        

==============================================================================
CONFIGURATION                                       *leader-guide-configuration*

|vim-leader-guide| uses the following configuration variables:

    ----------------------------------------------------------~
     VAR                                             DEFAULT ~
    ----------------------------------------------------------~
     |g:leaderGuide_position|                         botright
     |g:leaderGuide_vertical|                                0
     |g:leaderGuide_hspace|                                  5
     |g:leaderGuide_run_map_on_popup|                        1
     |g:leaderGuide_flatten|                                 1
     |g:leaderGuide_hide_in_display|           see description

                                                      *g:leaderGuide_position*
g:leaderGuide_position~
    Set the direction from witch the guide-buffer should pop up.

    Possible values:        `botright`, `topleft`
    Default:                `botright`

    Note: Use in conjunction with |g:leaderGuide_vertical| to set the
    buffer-position


                                                      *g:leaderGuide_vertical*
g:leaderGuide_vertical~
    If set to a value other than 0, the guide buffer will pop up in vertical
    split window.

    Possible values:        {0,1}
    Default:                0

    Note: Use in conjunction with |g:leaderGuide_position| to set the
    buffer-position


                                                        *g:leaderGuide_hspace*
g:leaderGuide_hspace~
    Sets the minimum horizontal space between the displayed colomns.

    The actual column width is the length of the longest entry plus this
    value.

    Possible values:        {Number}
    Default:                5


                                              *g:leaderGuide_run_map_on_popup*
g:leaderGuide_run_map_on_popup~
    If set to 0, |vim-leader-guide| will not parse mappings when opening a
    guide buffer. It will used cached values instead.

    Note: It is not recommended to disable this option. Mappings change during
    a vim-session, when changing the filetype or buffer for example.
    Thus, when disabling this option the displayed mappings might get
    outdated.

    Possible values:        {0,1}
    Default:                1

                                                       *g:leaderGuide_flatten*
g:leaderGuide_flatten~
    If set to 1, |vim-leader-guide| will try to flatten out the keymappings if
    necessary.

    Example:
        vim-surround (|https://github.com/tpope/vim-surround|) defines
        the following mappings by default: (among others)
        ys  -> <Plug>Ysurround
        yss -> <Plug>Yssurround

        Upon pressing `ys`, the first mapping is triggered when following up
        with a text object. The second one is triggered by following with
        another `s`.

        With the flatten option set, the guide (`:LeaderGuide` `'y'`) will
        display:
>
        [s]  <Plug>Ysurround       [ss] <Plug>Yssurround
<
        when the normal behaviour would put the [ss] mapping as [s] into the
        [s] subgroup.


    Note: With this option set to 0 mappings of the above type are not
    displayed correctly. Only the longest mappings will appear in the guide.

    Possible values:        {0,1}
    Default:                1

                                                   *g:leaderGuide_displayfunc*
g:leaderGuide_displayfunc~
    Sets a function to call on all for every displaystring in the guide
    buffer.
    
    Inside of the function |g:leaderGuide#displayname| is available as the
    string to display for the current mapping. It can be manipulated by
    the function.

    Example:
>
    function! s:my_displayfunc()
        let g:leaderGuide#displayname =
        \ substitute(g:leaderGuide#displayname, '\c<cr>$', '', '')
        let g:leaderGuide#displayname = 
        \ substitute(g:leaderGuide#displayname, '^<Plug>', '', '')
    endfunction
    let g:leaderGuide_displayfunc = [function("s:my_displayfunc")]
<
    The above settings will hide all trailing <CR> as well as all leading
    <Plug> substrings in the guide buffer.

    :so $MYVIMRC<CR> => :so $MYVIMRC
    <Plug>Ysurround  => Ysurround

    Possible values:        {list} of funcrefs
    Default: >
    function! s:leaderGuide_display()
        let g:leaderGuide#displayname =
        \ substitute(g:leaderGuide#displayname, '\c<cr>$', '', '')
    endfunction
<

                                                   *g:leaderGuide#displayname*
g:leaderGuide#displayname~
    This variable is set to the displayname (The string that will show up in
    the guide buffer) of a mapping. Only available in of display functions.
    See |g:leaderGuide_displayfunc|.

    Do not set this variable manually outside of a display function. It will
    be overwritten anyway.

==============================================================================
CHANGELOG                                               *leader-guide-changelog*

    2016-04-16:     - Add option to run functions on the displayname.
                      (g:|leaderGuide_displayfunc|)

    2016-04-12:     - Add option to flatten map<->group conflicts.
                      (g:|leaderGuide_flatten|)

    2016-04-10:     - Support for {count}, and register passthrough
                    - Rename g:leaderGuide_vspace -> g:leaderGuide_hspace
                    - Started documentation

==============================================================================
 vim:tw=78:ts=8:ft=help:norl:
